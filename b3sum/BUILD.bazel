load("@rules_rust//rust:defs.bzl", "rust_binary")
load("@crate_index//:defs.bzl", "aliases", "all_crate_deps")

load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")

rust_binary(
    name = "b3sum",
    srcs = glob(["src/**/*.rs"]),
    aliases = aliases(),
    crate_name = "b3sum",
    proc_macro_deps = all_crate_deps(proc_macro = True),
    deps = ["//:blake3"] + all_crate_deps(normal = True),
)

platform(
    name = "linux_arm64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:aarch64",
    ],
)

platform(
    name = "linux_amd64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

platform(
    name = "darwin_arm64",
    constraint_values = [
        "@platforms//os:macos",
        "@platforms//cpu:aarch64",
    ],
)

platform(
    name = "darwin_amd64",
    constraint_values = [
        "@platforms//os:macos",
        "@platforms//cpu:x86_64",
    ],
)

PLATFORMS = [
    ":linux_arm64",
    ":linux_amd64",
    ":darwin_arm64",
    ":darwin_amd64",
]

[
    platform_transition_filegroup(
        name = "for_" + platform.split(":")[1],
        srcs = [":b3sum"],
        target_platform = platform,
    )
    for platform in PLATFORMS
]

filegroup(
    name = "for_all_platforms",
    srcs = ["for_" + platform.split(":")[1] for platform in PLATFORMS],
)